# Copyright (c) 2023-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/appframework/ability/ability_runtime/ability_runtime.gni")

config("jsruntime_config") {
  include_dirs = [
    "//foundation/appframework/resource_management/interfaces/js/innerkits/core/include",
    "//base/global/resource_management/interfaces/inner_api/include",
    "//foundation/arkui/napi",
    "//third_party/flutter/engine",
    "//third_party/libuv/include",
    "${ability_runtime_cross_platform_path}/interfaces/inner_api/ability_manager",
    "${ability_runtime_cross_platform_path}/interfaces/inner_api/app_manager",
    "${ability_runtime_cross_platform_path}/interfaces/inner_api/bundle",
    "${ability_runtime_cross_platform_path}/interfaces/inner_api/configuration",
    "${ability_runtime_cross_platform_path}/interfaces/inner_api/error",
    "${ability_runtime_cross_platform_path}/interfaces/inner_api/jsruntime",
    "${ability_runtime_cross_platform_path}/interfaces/inner_api/want",
    "${ability_runtime_cross_platform_path}/interfaces/kits/native/ability",
    "${ability_runtime_cross_platform_path}/interfaces/kits/native/appkit",
    "${ability_runtime_cross_platform_path}/frameworks/native/base/event_handler/include",
    "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/include",
  ]
  include_dirs += [
    "$ark_toolchain_path",
    "$ark_toolchain_path/inspector",
    "$ark_toolchain_path/websocket",
    "${cross_platform_napi_path}/js_environment",
  ]
  if (target_os == "android") {
    include_dirs += [
      "//foundation/arkui/ace_engine/adapter/android/entrance/java/jni",
      "//foundation/arkui/ace_engine/adapter/android/stage/ability/java/jni",
    ]
  }
}

template("ability_runtime_jsruntime") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    subsystem_name = ability_subsystem_name
    part_name = ability_part_name
    configs = [
      ":jsruntime_config",
      "//arkcompiler/ets_runtime:ark_jsruntime_public_config",
      "${ability_runtime_cross_platform_path}/interfaces/inner_api/bundle:bundle_parser_config",
      "${ability_runtime_cross_platform_path}/interfaces/inner_api/hilog:ability_cross_platform_hilog_config",
    ]

    defines = [
      "SUPPORT_GRAPHICS",
      "__ARKUI_CROSS__",
    ]

    if (target_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }

    sources = [
      "${ability_runtime_cross_platform_path}/frameworks/native/base/error/ability_business_error.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_aot_reader.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_console_log.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_data_struct_converter.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_error_utils.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_module_reader.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_runtime.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_runtime_utils.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_timer.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/js_worker.cpp",
      "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/runtime.cpp",
    ]

    if (runtime_mode != "release") {
      sources += [ "${ability_runtime_cross_platform_path}/frameworks/native/jsruntime/src/connect_server_manager.cpp" ]
      defines += [ "DEBUG_MODE" ]
    }

    deps = [
      "${ability_runtime_cross_platform_path}/interfaces/inner_api/bundle:cross_platform_bundle_parser",
      "${cross_platform_napi_path}/js_environment:cross_platform_js_environment",
      "//foundation/appframework/resource_management/interfaces/js/innerkits/core:resmgr_napi_core",
      "//foundation/arkui/napi:ace_napi",
    ]

    if (is_arkui_x) {
      defines += [ "CROSS_PLATFORM" ]
      include_dirs = [
        "${cross_platform_napi_path}/js_environment/uncaught_exception_callback.h",
        "//commonlibrary/ets_utils/js_concurrent_module/common",
      ]
    }

    if (target_os == "android") {
      defines += [ "ANDROID_PLATFORM" ]
    } else if (target_os == "ios") {
      defines += [ "IOS_PLATFORM" ]
    }
    deps += [ "//arkcompiler/ets_runtime:libark_jsruntime_static" ]

    deps +=
        [ "//foundation/appframework/arkui/uicontent:ace_uicontent_interface" ]
  }
}

ability_runtime_jsruntime("cross_platform_jsruntime") {
}
