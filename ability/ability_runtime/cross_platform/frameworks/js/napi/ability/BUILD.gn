# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/es2panda/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")
import("//foundation/appframework/ability/ability_runtime/ability_runtime.gni")

es2abc_gen_abc("gen_ability_abc") {
  extra_visibility = [ ":*" ]  # Only targets in this file can depend on this.
  src_js = rebase_path("ability.js")
  dst_file = rebase_path(target_out_dir + "/ability.abc")

  in_puts = [ "ability.js" ]
  out_puts = [ target_out_dir + "/ability.abc" ]
  extra_args = [ "--module" ]
}

gen_obj("ability_js") {
  input = "ability.js"
  if (target_os == "ios") {
    output = target_out_dir + "/ability.c"
  } else {
    output = target_out_dir + "/ability.o"
  }
  snapshot_dep = []
}

gen_obj("ability_abc") {
  input = get_label_info(":gen_ability_abc", "target_out_dir") + "/ability.abc"
  if (target_os == "ios") {
    output = target_out_dir + "/ability_abc.c"
  } else {
    output = target_out_dir + "/ability_abc.o"
  }
  snapshot_dep = [ ":gen_ability_abc" ]
}

ohos_source_set("app_ability_uiability_source_set") {
  sources = [ "ability_module.cpp" ]

  deps = [
    ":gen_obj_src_ability_abc",
    ":gen_obj_src_ability_js",
  ]
  if (use_static_napi_libs) {
    deps += [ "//foundation/arkui/napi:ace_napi" ]
  } else {
    deps += ["//plugins/libs/napi:napi_${target_os}"]
  }

  if (target_os == "ios") {
    defines = [ "IOS_PLATFORM" ]
  }

  subsystem_name = "ability"
  part_name = "ability_runtime"
}

ohos_shared_library("app_ability_uiability") {
  deps = [ ":app_ability_uiability_source_set" ]

  relative_install_dir = "module/app/ability"
  subsystem_name = "ability"
  part_name = "ability_runtime"
}
