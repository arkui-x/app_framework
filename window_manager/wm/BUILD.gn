# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/appframework/window_manager/window_manager.gni")

config("window_manager_config") {
  include_dirs = [
    "${app_framework_window_manager_path}/wm/include",
    "${window_manager_napi_path}/innerkits/wm",
    "${window_manager_napi_path}/kits/kits/napi/window_runtime",
  ]
}

template("window_manager") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    subsystem_name = window_manager_subsystem_name
    part_name = window_manager_part_name
    configs = [
      ":window_manager_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/ability:abilitykit_native_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/inner_api/hilog:ability_cross_platform_hilog_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/appkit:appkit_native_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/inner_api/jsruntime:jsruntime_config",
      "$ace_root:ace_config",
    ]
    defines = []
    if (target_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }

    include_dirs = [
      "${app_framework_window_manager_path}/wm/include",
      "${window_manager_napi_path}/innerkits/wm",
      "${window_manager_napi_path}/kits/kits/napi/window_runtime",
      "//commonlibrary/c_utils/base/include",
      "../utils/include",
    ]

    if (is_arkui_x) {
      include_dirs += [
        "//foundation/graphic/graphic_surface/interfaces/inner_api/surface",
      ]
    }

    sources = [
      "${app_framework_window_manager_path}/wm/src/window_option.cpp",
      "${app_framework_window_manager_path}/wm/src/window_stage.cpp",
    ]

    deps = [ "${window_manager_napi_path}/kits/napi/window_runtime:wm_napi" ]

    if (current_os == "android") {
      include_dirs +=
          [ "//foundation/arkui/ace_engine/adapter/android/entrance/java/jni" ]
      deps += [ "//foundation/arkui/ace_engine/adapter/android/entrance/java/jni:ace_common_jni_android" ]
    } else if (current_os == "ios") {
      defines += [ "IOS_PLATFORM" ]
      include_dirs += [ "//foundation/arkui/ace_engine/adapter/ios/entrance" ]
      deps += [
        "//foundation/arkui/ace_engine/adapter/ios/entrance:ace_ios_entrance",
      ]
    }

    deps += [
      "//foundation/appframework/arkui/uicontent:ace_uicontent_interface",
      "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client_static",
    ]
  }
}

window_manager("wm") {
}
