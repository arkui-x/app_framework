# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

#import("//arkcompiler/ets_frontend/ts2panda/ts2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/appframework/window_manager/window_manager.gni")
config("display_manager_napi_config") {
  include_dirs = [
    "${app_framework_window_manager_path}/dm/include",
    "${display_manager_napi_path}/innerkits/dm",
    "//arkcompiler/runtime_core/common_interfaces",
  ]
}

template("display_manager_napi") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    subsystem_name = display_manager_subsystem_name
    part_name = display_manager_part_name
    configs = [
      ":display_manager_napi_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/ability:abilitykit_native_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/inner_api/hilog:ability_cross_platform_hilog_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/kits/native/appkit:appkit_native_config",
      "//foundation/appframework/ability/ability_runtime/cross_platform/interfaces/inner_api/jsruntime:jsruntime_config",
      "$ace_root:ace_config",
      "//foundation/arkui/napi:ace_napi_config",
    ]
    defines = []
    if (target_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }

    include_dirs = [
      "${display_manager_napi_path}/dm/include",
      "${display_manager_napi_path}/innerkits/dm",
      "${display_manager_napi_path}/kits/napi/display_runtime",
      "//arkcompiler/ets_runtime",
      "//arkcompiler/runtime_core",
      "//commonlibrary/c_utils/base/include",
      "//foundation/appframework/window_manager/utils/include",
    ]
    deps = [ "//foundation/appframework/window_manager/utils:wm_utils" ]
    if (current_os == "android") {
      include_dirs +=
          [ "//foundation/arkui/ace_engine/adapter/android/entrance/java/jni" ]
    } else if (current_os == "ios") {
      defines += [ "IOS_PLATFORM" ]
      include_dirs += [ "//foundation/arkui/ace_engine/adapter/ios/entrance" ]
    }

    sources = [
      "${display_manager_napi_path}/kits/napi/display_runtime/display_manager_module.cpp",
      "${display_manager_napi_path}/kits/napi/display_runtime/js_display.cpp",
      "${display_manager_napi_path}/kits/napi/display_runtime/js_display_listener.cpp",
      "${display_manager_napi_path}/kits/napi/display_runtime/js_display_manager.cpp",
    ]
  }
}

display_manager_napi("dm_napi") {
}
